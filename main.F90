PROGRAM MAIN
  USE YOMHOOK, ONLY: LHOOK
  IMPLICIT NONE
  INTEGER :: NPROMA, LAST_BLK_NPROMA, NFLEV, NBLKS, GRID, NSTEPMAX
  CHARACTER(LEN=1) :: ENV_LHOOK
  CHARACTER(LEN=8) :: ARG
  CHARACTER(LEN=1024) :: DATA_PATH
  LOGICAL :: FILE_EXIST

  CALL GET_ENVIRONMENT_VARIABLE("LHOOK", ENV_LHOOK)
  IF(ENV_LHOOK == "1") LHOOK=.TRUE.

  IF(COMMAND_ARGUMENT_COUNT() /= 3)THEN
    CALL USAGE()
    STOP 1
  ENDIF

  CALL GET_COMMAND_ARGUMENT(1, DATA_PATH)
  CALL GET_COMMAND_ARGUMENT(2, ARG)
  READ(ARG, *) NPROMA
  CALL GET_COMMAND_ARGUMENT(3, ARG)
  READ(ARG, *) NSTEPMAX

  INQUIRE(FILE=TRIM(DATA_PATH)//"/ACVPPKF.IN.DAT", EXIST=FILE_EXIST)
  IF(.NOT. FILE_EXIST)THEN
    WRITE(*,*)"COULDN'T FIND INPUT FILE DATA/ACVPPKF.IN.DAT"
    STOP 1
  ENDIF
  INQUIRE(FILE=TRIM(DATA_PATH)//"/ACVPPKF.OUT.DAT", EXIST=FILE_EXIST)
  IF(.NOT. FILE_EXIST)THEN
    WRITE(*,*)"COULDN'T FIND INPUT FILE DATA/ACVPPKF.OUT.DAT"
    STOP 1
  ENDIF
  INQUIRE(FILE=TRIM(DATA_PATH)//"/ACVPPKF.CONST.DAT", EXIST=FILE_EXIST)
  IF(.NOT. FILE_EXIST)THEN
    WRITE(*,*)"COULDN'T FIND INPUT FILE DATA/ACVPPKF.CONST.DAT"
    STOP 1
  ENDIF

  CALL GET_GRID_AND_NFLEV(GRID, NFLEV, DATA_PATH)
  IF(MOD(GRID, NPROMA) ==0)THEN
    NBLKS=GRID/NPROMA
    LAST_BLK_NPROMA=NPROMA
  ELSE
    NBLKS=GRID/NPROMA +1
    LAST_BLK_NPROMA=MOD(GRID, NPROMA)
  ENDIF

  WRITE(*,*)"DATA PATH           : ", TRIM(DATA_PATH)
  WRITE(*,*)"NSTEPMAX            : ", NSTEPMAX
  WRITE(*,*)"GRID SIZE           : ", GRID
  WRITE(*,*)"NPROMA              : ", NPROMA
  WRITE(*,*)"NBLKS               : ", NBLKS
  WRITE(*,*)"NPROMA OF LAST BLOCK: ", LAST_BLK_NPROMA

  CALL WRAP_ACVPPKF(DATA_PATH, NSTEPMAX, NPROMA, LAST_BLK_NPROMA, NFLEV, NBLKS, GRID)
  WRITE(*,*)"ALL GOOD"
END PROGRAM MAIN

SUBROUTINE GET_GRID_AND_NFLEV(GRID, NFLEV, DATA_PATH)
  IMPLICIT NONE
  INTEGER, INTENT(OUT) :: GRID, NFLEV
  CHARACTER(LEN=1024), INTENT(IN) :: DATA_PATH
  INTEGER :: FH, CHECK
  INTEGER, PARAMETER :: CHECK_VAL = 123456789

  OPEN (NEWUNIT=FH, FILE=TRIM(DATA_PATH)//"/ACVPPKF.CONST.DAT", ACTION="READ", ACCESS="STREAM")
  READ(FH)CHECK
  IF(CHECK /= CHECK_VAL)WRITE(*,*)__LINE__,"WRONG CONTROL NUMBER",CHECK
  READ(FH)GRID
  READ(FH)NFLEV
  CLOSE(FH)
END SUBROUTINE GET_GRID_AND_NFLEV

SUBROUTINE USAGE()
  WRITE(*,*)"Usage     : ./main.x DATA_PATH NPROMA NSTEPMAX"
  WRITE(*,*)
  WRITE(*,*)"DATA_PATH : path to the directory containing the input &
             &files for the simulation"
  WRITE(*,*)"NPROMA    : the ideal number of points per block"
  WRITE(*,*)"NSTEPMAX  : the number of step to run the simulation"
END SUBROUTINE USAGE
